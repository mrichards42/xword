; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "XWord"
!define PRODUCT_VERSION "0.4"
!define PRODUCT_WEB_SITE "http://sourceforge.net/projects/wx-xword/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\XWord.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"

; SetCompressor lzma

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "D:\C++\XWord\trunk\COPYING"

; Portable page
LangString PORTABLE_PAGE_TITLE ${LANG_ENGLISH} "Choose Install Type"
LangString PORTABLE_PAGE_SUBTITLE ${LANG_ENGLISH} "Choose standard installation or portable installation."

!include "nsDialogs.nsh"
!include "winmessages.nsh"
!include "logiclib.nsh"

Page Custom PortablePre PortablePost

var dialog
var hwnd
var isPortable
var isInstallDirSet
var AUXDIR

Function PortablePre
         ; Default value for $isPortable
         ${If} $isPortable == ""
                  StrCpy $isPortable "false"
         ${EndIf}

             !insertmacro MUI_HEADER_TEXT $(PORTABLE_PAGE_TITLE) $(PORTABLE_PAGE_SUBTITLE)
	nsDialogs::Create 1018
		Pop $dialog

        ; Radio buttons
	${NSD_CreateRadioButton} 0 0 40% 6% "Standard Installation"
		Pop $hwnd
		${NSD_AddStyle} $hwnd ${WS_GROUP}
		nsDialogs::SetUserData $hwnd "false"
		${NSD_OnClick} $hwnd RadioClick
                ; Is this initially selected?
                ${If} $isPortable == "false"
                      ${NSD_Check} $hwnd
                ${EndIf}
	${NSD_CreateRadioButton} 0 12% 40% 6% "Portable Installation"
		Pop $hwnd
		nsDialogs::SetUserData $hwnd "true"
		${NSD_OnClick} $hwnd RadioClick
                ; Is this initially selected?
                ${If} $isPortable == "true"
                      ${NSD_Check} $hwnd
                ${EndIf}

	nsDialogs::Show
FunctionEnd

Function RadioClick
	Pop $hwnd
	nsDialogs::GetUserData $hwnd
	Pop $isPortable
FunctionEnd

Function PortablePost
         ; Set the default install dir based based on the selection.
         ; Only do this the first time we visit the page
         ${If} $isInstallDirSet == ""
        	${If} $isPortable == "true"
                	StrCpy $INSTDIR "$DESKTOP\XWord"
                ${Else}
                        StrCpy $INSTDIR "$PROGRAMFILES\XWord"
                ${EndIf}
                StrCpy $isInstallDirSet "true"
        ${EndIf}

        ; Set the auxiliary files folder
        ${If} $isPortable == "true"
              StrCpy $AUXDIR "$INSTDIR"
        ${Else}
               StrCpy $AUXDIR "$APPDATA\XWord"
        ${EndIf}
FunctionEnd

; End Portable page



; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "XWord"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!define MUI_PAGE_CUSTOMFUNCTION_PRE startMenuPagePre
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
Function startMenuPagePre
         ; No need for start menu icons if this is a portable installation
         ${If} $isPortable == "true"
               Abort
         ${EndIf}
FunctionEnd

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\XWord.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup.exe"
InstallDir "$PROGRAMFILES\XWord" ; This will be overwritten by the portable page
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  File "D:\C++\XWord\trunk\bin\Debug\XWord.exe"
  File "D:\C++\XWord\trunk\bin\Debug\lua5.1.dll"
  File "D:\C++\XWord\trunk\bin\Debug\libcurl.dll"

  SetOutPath "$AUXDIR\config"
  File "D:\C++\XWord\trunk\bin\Debug\config\config.ini"

  SetOutPath "$INSTDIR\images"
  File "D:\C++\XWord\trunk\images\check_grid_24.png"
  File "D:\C++\XWord\trunk\images\check_letter_16.png"
  File "D:\C++\XWord\trunk\images\check_letter_24.png"
  File "D:\C++\XWord\trunk\images\check_word_16.png"
  File "D:\C++\XWord\trunk\images\check_word_24.png"
  File "D:\C++\XWord\trunk\images\layout_16.png"
  File "D:\C++\XWord\trunk\images\layout_24.png"
  File "D:\C++\XWord\trunk\images\notes_16.png"
  File "D:\C++\XWord\trunk\images\notes_24.png"
  File "D:\C++\XWord\trunk\images\notes_new_16.png"
  File "D:\C++\XWord\trunk\images\notes_new_24.png"
  File "D:\C++\XWord\trunk\images\open_16.png"
  File "D:\C++\XWord\trunk\images\open_24.png"
  File "D:\C++\XWord\trunk\images\save_16.png"
  File "D:\C++\XWord\trunk\images\save_24.png"
  File "D:\C++\XWord\trunk\images\timer_16.png"
  File "D:\C++\XWord\trunk\images\timer_24.png"
  File "D:\C++\XWord\trunk\images\zoom_fit_16.png"
  File "D:\C++\XWord\trunk\images\zoom_fit_24.png"
  File "D:\C++\XWord\trunk\images\zoom_in_16.png"
  File "D:\C++\XWord\trunk\images\zoom_in_24.png"
  File "D:\C++\XWord\trunk\images\zoom_out_16.png"
  File "D:\C++\XWord\trunk\images\zoom_out_24.png"

  SetOutPath "$AUXDIR\scripts"
  File "D:\C++\XWord\trunk\scripts\oneacross.lua"
  File "D:\C++\XWord\trunk\scripts\swap.lua"
  File "D:\C++\XWord\trunk\scripts\wikipedia.lua"

  SetOutPath "$AUXDIR\scripts\xword"
  File "D:\C++\XWord\trunk\scripts\xword\init.lua"
  File "D:\C++\XWord\trunk\scripts\xword\messages.lua"
  File "D:\C++\XWord\trunk\scripts\xword\utf-8.lua"
  File "D:\C++\XWord\trunk\scripts\xword\utils.lua"

  SetOutPath "$AUXDIR\scripts\libs"
  File "D:\C++\XWord\trunk\scripts\libs\lfs.dll"
  File "D:\C++\XWord\trunk\scripts\libs\task.dll"
  File "D:\C++\XWord\trunk\scripts\libs\date.lua"
  File "D:\C++\XWord\trunk\scripts\libs\luacurl.dll"
  File "D:\C++\XWord\trunk\scripts\libs\mtask.lua"
  File "D:\C++\XWord\trunk\scripts\libs\queue.lua"
  File "D:\C++\XWord\trunk\scripts\libs\serialize.lua"

  SetOutPath "$AUXDIR\scripts\download"
  File "D:\C++\XWord\trunk\scripts\download\init.lua"
  File "D:\C++\XWord\trunk\scripts\download\bmp.lua"
  File "D:\C++\XWord\trunk\scripts\download\ctrl.lua"
  File "D:\C++\XWord\trunk\scripts\download\defaultconfig.lua"
  File "D:\C++\XWord\trunk\scripts\download\defaultsources.lua"
  File "D:\C++\XWord\trunk\scripts\download\defs.lua"
  File "D:\C++\XWord\trunk\scripts\download\dialog.lua"
  File "D:\C++\XWord\trunk\scripts\download\dltable.lua"
  File "D:\C++\XWord\trunk\scripts\download\task.lua"
  SetOutPath "$AUXDIR\scripts\download\images"
  File "D:\C++\XWord\trunk\scripts\download\images\download.png"
  File "D:\C++\XWord\trunk\scripts\download\images\downloaderror.png"
  File "D:\C++\XWord\trunk\scripts\download\images\play.png"
  File "D:\C++\XWord\trunk\scripts\download\images\swap.png"
  SetOutPath "$AUXDIR\scripts\download\layout"
  File "D:\C++\XWord\trunk\scripts\download\layout\grid.lua"
  File "D:\C++\XWord\trunk\scripts\download\layout\init.lua"
  File "D:\C++\XWord\trunk\scripts\download\layout\puzzle.lua"

  SetOutPath "$AUXDIR\scripts\import"
  File "D:\C++\XWord\trunk\scripts\import\init.lua"
  File "D:\C++\XWord\trunk\scripts\import\xml-uclick.lua"
  File "D:\C++\XWord\trunk\scripts\import\xpf.lua"

  ; If it's portable, make a portable_mode_enabled file.
  ${If} $isPortable == "true"
        FileOpen $0 "$INSTDIR\portable_mode_enabled" w
        FileClose $0
  ${EndIf}

; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  ${If} $isPortable != "true"
          CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
          CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\XWord.lnk" "$INSTDIR\XWord.exe"
          CreateShortCut "$DESKTOP\XWord.lnk" "$INSTDIR\XWord.exe"
          ${EndIf}
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -AdditionalIcons
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  ${If} $isPortable != "true"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
  ${EndIf}
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -Post
${If} $isPortable != "true"
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\XWord.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\XWord.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "AuxFilesDir" "$AUXDIR"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "InstDir" "$INSTDIR"
${EndIf}
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  !insertmacro MUI_STARTMENU_GETFOLDER "Application" $ICONS_GROUP
  ReadRegStr $AUXDIR  ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "AuxFilesDir"
  ReadRegStr $INSTDIR ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "InstDir"

  ; Remove everything in this folder
  RmDir /r "$APPDATA\XWord"

  ; If we installed in a proper location, it's somewhat cleaner
  ; to delete the entire directory, since other files may have
  ; been created.
  ${If} $INSTDIR == "$PROGRAMFILES\XWord"
        RmDir /r $INSTDIR
  ${Else}

  Delete "$INSTDIR\uninst.exe"

  Delete "$INSTDIR\XWord.exe"
  Delete "$INSTDIR\lua5.1.dll"
  Delete "$INSTDIR\libcurl.dll"
  Delete "$INSTDIR\config\config.ini"
  Delete "$INSTDIR\images\check_grid_24.png"
  Delete "$INSTDIR\images\check_letter_16.png"
  Delete "$INSTDIR\images\check_letter_24.png"
  Delete "$INSTDIR\images\check_word_16.png"
  Delete "$INSTDIR\images\check_word_24.png"
  Delete "$INSTDIR\images\layout_16.png"
  Delete "$INSTDIR\images\layout_24.png"
  Delete "$INSTDIR\images\notes_16.png"
  Delete "$INSTDIR\images\notes_24.png"
  Delete "$INSTDIR\images\notes_new_16.png"
  Delete "$INSTDIR\images\notes_new_24.png"
  Delete "$INSTDIR\images\open_16.png"
  Delete "$INSTDIR\images\open_24.png"
  Delete "$INSTDIR\images\save_16.png"
  Delete "$INSTDIR\images\save_24.png"
  Delete "$INSTDIR\images\timer_16.png"
  Delete "$INSTDIR\images\timer_24.png"
  Delete "$INSTDIR\images\xword.ico"
  Delete "$INSTDIR\images\zoom_fit_16.png"
  Delete "$INSTDIR\images\zoom_fit_24.png"
  Delete "$INSTDIR\images\zoom_in_16.png"
  Delete "$INSTDIR\images\zoom_in_24.png"
  Delete "$INSTDIR\images\zoom_out_16.png"
  Delete "$INSTDIR\images\zoom_out_24.png"
  Delete "$INSTDIR\scripts\oneacross.lua"
  Delete "$INSTDIR\scripts\swap.lua"
  Delete "$INSTDIR\scripts\wikipedia.lua"
  Delete "$INSTDIR\scripts\xword\init.lua"
  Delete "$INSTDIR\scripts\xword\messages.lua"
  Delete "$INSTDIR\scripts\xword\utf-8.lua"
  Delete "$INSTDIR\scripts\xword\utils.lua"
  Delete "$INSTDIR\scripts\libs\lfs.dll"
  Delete "$INSTDIR\scripts\libs\task.dll"
  Delete "$INSTDIR\scripts\libs\date.lua"
  Delete "$INSTDIR\scripts\libs\luacurl.dll"
  Delete "$INSTDIR\scripts\libs\mtask.lua"
  Delete "$INSTDIR\scripts\libs\queue.lua"
  Delete "$INSTDIR\scripts\libs\serialize.lua"
  Delete "$INSTDIR\scripts\download\init.lua"
  Delete "$INSTDIR\scripts\download\bmp.lua"
  Delete "$INSTDIR\scripts\download\ctrl.lua"
  Delete "$INSTDIR\scripts\download\defaultconfig.lua"
  Delete "$INSTDIR\scripts\download\defaultsources.lua"
  Delete "$INSTDIR\scripts\download\defs.lua"
  Delete "$INSTDIR\scripts\download\dialog.lua"
  Delete "$INSTDIR\scripts\download\dltable.lua"
  Delete "$INSTDIR\scripts\download\task.lua"
  Delete "$INSTDIR\scripts\download\images\download.png"
  Delete "$INSTDIR\scripts\download\images\swap.png"
  Delete "$INSTDIR\scripts\download\layout\grid.lua"
  Delete "$INSTDIR\scripts\download\layout\init.lua"
  Delete "$INSTDIR\scripts\download\layout\puzzle.lua"
  Delete "$INSTDIR\scripts\import\init.lua"
  Delete "$INSTDIR\scripts\import\xml-uclick.lua"
  Delete "$INSTDIR\scripts\import\xpf.lua"

  RMDir "$INSTDIR\scripts\xworddebug"
  RMDir "$INSTDIR\scripts\xword"
  RMDir "$INSTDIR\scripts\libs"
  RMDir "$INSTDIR\scripts\import"
  RMDir "$INSTDIR\scripts\download\puzzles"
  RMDir "$INSTDIR\scripts\download\layout"
  RMDir "$INSTDIR\scripts\download\images"
  RMDir "$INSTDIR\scripts\download"
  RMDir "$INSTDIR\scripts"
  RMDir "$INSTDIR\images"
  RMDir "$INSTDIR\config"
  RMDir "$INSTDIR"
  ${EndIf}

  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$DESKTOP\XWord.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\XWord.lnk"
  RMDir "$SMPROGRAMS\$ICONS_GROUP"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose false
SectionEnd